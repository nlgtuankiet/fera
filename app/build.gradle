apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "androidx.navigation.safeargs.kotlin"

apply from: "../gradle/config.gradle"

kapt {
  correctErrorTypes true
}
def production = hasProperty("production")

android {
  compileSdkVersion 29
  buildToolsVersion "29.0.3"
  testOptions {
    unitTests.all {
      testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
        outputs.upToDateWhen { false }
        showStandardStreams = true
        exceptionFormat = 'full'
      }
    }
  }
  defaultConfig {
    applicationId "com.nlgtuankiet.fera"
    minSdkVersion minSdk
    targetSdkVersion targetSdk
    versionCode vcode
    versionName vname
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled true
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
    }
  }


  sourceSets {
    androidTest {
      java.srcDirs += ['src/test/java/com/nlgtuankiet/fera/share']
    }
  }
  if (!production) {
    dynamicFeatures += ":home"
  }
}

dependencies {
  implementation deps.moshi
  kapt deps.moshiProcessor
  implementation deps.dagger
  kapt deps.daggerProcessor
  api deps.exec
  api deps.execKiller
  api deps.kotlinStdLib
  api deps.kotlinCoroutine
  api deps.navigation
  api deps.navigationDynamic

  api deps.daggerAndroid
  api(project(":domain"))
  api(project(":core"))
  if (production) {
    api(project(":home"))
  }
  implementation("androidx.core:core-ktx:1.3.0")
  implementation("androidx.multidex:multidex:2.0.1")
//  implementation 'com.arthenica:mobile-ffmpeg-full:4.3.2'
  api deps.appCompat
  api deps.fragment
  api deps.constraintLayout
  api deps.inject
  testImplementation("junit:junit:4.12")
  androidTestImplementation("androidx.test.ext:junit:1.1.1")
  androidTestImplementation("androidx.test.espresso:espresso-core:3.2.0")
}

